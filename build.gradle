// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'buildsystem/constants.gradle'
    apply from: 'buildsystem/helpers.gradle'
    apply from: 'buildsystem/dependencies.gradle'

    repositories {
        google()
        jcenter()
        maven { url 'https://www.jitpack.io' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.android_tools"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.gnosis.bivrost-kotlin:bivrost-gradle-plugin:$versions.bivrost"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 33

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 33
                }

                lintOptions {
                    abortOnError false
                }
            }

            task androidSourcesJar(type: Jar) {
                from android.sourceSets.main.java.source
                classifier = 'sources'
            }

            artifacts {
                archives androidSourcesJar
            }
        } else {
            task sourcesJar(type: Jar, dependsOn:classes) {
                from sourceSets.main.allSource
                classifier = 'sources'
            }

            artifacts {
                archives sourcesJar
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
